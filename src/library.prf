include(../project.prf)

# Qt 4.8.3 has a qmake bug that can be worked around by adding "dll" to CONFIG.
win32-g++: CONFIG *= dll

win32: DLLDESTDIR = $$PRO_APP_PATH
DESTDIR = $$PRO_LIBRARY_PATH

include(../src/rpath.prf)

!macx {
    CONFIG += plugin plugin_with_soname
    contains(QT_CONFIG, reduce_exports):CONFIG += hide_symbols
    win32 {
        target.path = /bin
        target.files = $$DESTDIR/$${TARGET}.dll
    } else {
        target.path = /$$PRO_LIBRARY_BASENAME/$$PRO_APP_TARGET
    }
    INSTALLS += target
}
