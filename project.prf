!isEmpty(PROJECT_PRI_INCLUDED): error("$$_FILE_ already included")
PROJECT_PRI_INCLUDED = 1

PRO_NAME           = AudioCarver
PRO_VERSION        = 1.0.0
PRO_AUTHOR         = Andrew Fillebrown
PRO_AUTHOR_NOSPACE = AndyFillebrown
PRO_AUTHOR_EMAIL   = andy.fillebrown@gmail.com
PRO_YEAR           = 2012
PRO_URL            = http://gitorious.org/audiocarver

DEFINES *= PRO_NAME_STR=\\\"$$PRO_NAME\\\"
DEFINES *= PRO_NAME_LC_STR=\\\"$$lower($$PRO_NAME)\\\"
DEFINES *= PRO_AUTHOR_NOSPACE_STR=\\\"$$PRO_AUTHOR_NOSPACE\\\"
DEFINES *= PRO_YEAR_STR=\\\"$$PRO_YEAR\\\"

defineReplace(cleanPath) {
    win32: 1 ~= s|\\\\|/|g
    contains(1, ^/.*): pfx = /
    else:pfx =
    segs = $$split(1, /)
    out =
    for(seg, segs) {
        equals(seg, ..): out = $$member(out, 0, -2)
        else: !equals(seg, .):out += $$seg
    }
    return($$join(out, /, $$pfx))
}

defineReplace(targetPath) {
    return($$replace(1, /, $$QMAKE_DIR_SEP))
}

defineReplace(qtLibraryName) {
   unset(LIBRARY_NAME)
   LIBRARY_NAME = $$1
   CONFIG(debug, debug|release) {
      !debug_and_release|build_pass {
          mac:RET = $$member(LIBRARY_NAME, 0)_debug
              else:win32:RET = $$member(LIBRARY_NAME, 0)d
      }
   }
   isEmpty(RET): RET = $$LIBRARY_NAME
   return($$RET)
}

defineTest(minQtVersion) {
    maj = $$1
    min = $$2
    patch = $$3
    isEqual(QT_MAJOR_VERSION, $$maj) {
        isEqual(QT_MINOR_VERSION, $$min) {
            isEqual(QT_PATCH_VERSION, $$patch) {
                return(true)
            }
            greaterThan(QT_PATCH_VERSION, $$patch) {
                return(true)
            }
        }
        greaterThan(QT_MINOR_VERSION, $$min) {
            return(true)
        }
    }
    return(false)
}

isEmpty(TARGET): error("$$_FILE_: You must provide a TARGET")
PRO_TARGET = $$TARGET
TARGET = $$qtLibraryName($$TARGET)

# For use in custom compilers that only copy files
win32:i_flag = i
defineReplace(stripDir) {
    return($$basename(1))
}

isEmpty(PRO_LIBRARY_BASENAME): PRO_LIBRARY_BASENAME = lib
DEFINES *= PRO_LIBRARY_BASENAME=\\\"$$PRO_LIBRARY_BASENAME\\\"

PRO_SOURCE_TREE = $$PWD
isEmpty(PRO_BUILD_TREE) {
    pro_dir = $$cleanPath($$_PRO_FILE_PWD_)
    sub_dir = $$cleanPath($$OUT_PWD)
    pro_dir_split = $$split(pro_dir, /)
    sub_dir_split = $$split(sub_dir, /)
    for(dir, sub_dir_split) {
        last_pro_dir = $$last(pro_dir_split)
        last_sub_dir = $$last(sub_dir_split)
        isEqual(last_pro_dir, $$last_sub_dir) {
            pro_dir = $$dirname(pro_dir)
            sub_dir = $$dirname(sub_dir)
            isEqual(sub_dir, $$PRO_SOURCE_TREE) {
                break()
            }
            pro_dir_split = $$split(pro_dir, /)
            sub_dir_split = $$split(sub_dir, /)
        } else {
            break()
        }
    }
    PRO_BUILD_TREE = $$sub_dir
}
PRO_APP_PATH = $$PRO_BUILD_TREE/bin
macx {
    PRO_APP_TARGET   = $$PRO_NAME
    PRO_LIBRARY_PATH = $$PRO_APP_PATH/$${PRO_APP_TARGET}.app/Contents/PlugIns
    PRO_PLUGIN_PATH  = $$PRO_LIBRARY_PATH
    PRO_LIBEXEC_PATH = $$PRO_APP_PATH/$${PRO_APP_TARGET}.app/Contents/Resources
    PRO_DATA_PATH    = $$PRO_APP_PATH/$${PRO_APP_TARGET}.app/Contents/Resources
    PRO_DOC_PATH     = $$PRO_DATA_PATH/doc
    PRO_BIN_PATH     = $$PRO_APP_PATH/$${PRO_APP_TARGET}.app/Contents/MacOS
    contains(QT_CONFIG, ppc): CONFIG += ppc x86
    copydata = 1
    isEmpty(TIGER_COMPAT_MODE): TIGER_COMPAT_MODE=$$(QTC_TIGER_COMPAT)
    isEmpty(TIGER_COMPAT_MODE) {
        QMAKE_CXXFLAGS *= -mmacosx-version-min=10.5
        QMAKE_LFLAGS *= -mmacosx-version-min=10.5
    }
} else {
    win32 {
        contains(TEMPLATE, vc.*)|contains(TEMPLATE_PREFIX, vc):vcproj = 1
    }
    PRO_APP_TARGET = $$lower($$PRO_NAME)
    PRO_LIBRARY_PATH = $$PRO_BUILD_TREE/$$PRO_LIBRARY_BASENAME/$$PRO_APP_TARGET
    PRO_PLUGIN_PATH  = $$PRO_LIBRARY_PATH/plugins
    PRO_LIBEXEC_PATH = $$PRO_APP_PATH
    PRO_DATA_PATH    = $$PRO_BUILD_TREE/share/$$PRO_APP_TARGET
    PRO_DOC_PATH     = $$PRO_BUILD_TREE/share/doc/$$PRO_APP_TARGET
    PRO_BIN_PATH     = $$PRO_APP_PATH
    !isEqual(PRO_SOURCE_TREE, $$PRO_BUILD_TREE): copydata = 1
}

INCLUDEPATH += $$PRO_SOURCE_TREE/src/libs
DEPENDPATH += $$PRO_SOURCE_TREE/src/libs

LIBS += -L$$PRO_LIBRARY_PATH

DEFINES *= QT_NO_CAST_TO_ASCII
!macx :DEFINES *= QT_USE_FAST_OPERATOR_PLUS QT_USE_FAST_CONCATENATION

unix {
    CONFIG(debug, debug|release): OBJECTS_DIR = $${OUT_PWD}/.obj/debug-shared
    CONFIG(release, debug|release): OBJECTS_DIR = $${OUT_PWD}/.obj/release-shared

    CONFIG(debug, debug|release): MOC_DIR = $${OUT_PWD}/.moc/debug-shared
    CONFIG(release, debug|release): MOC_DIR = $${OUT_PWD}/.moc/release-shared

    RCC_DIR = $${OUT_PWD}/.rcc
    UI_DIR = $${OUT_PWD}/.uic
}

linux-g++-* {
    # Bail out on non-selfcontained libraries. Just a security measure
    # to prevent checking in code that does not compile on other platforms.
    QMAKE_LFLAGS += -Wl,--allow-shlib-undefined -Wl,--no-undefined
}

win32-msvc* {
    #Don't warn about sprintf, fopen etc being 'unsafe'
    DEFINES *= _CRT_SECURE_NO_WARNINGS
}

# Handle S60 support: default on Windows, conditionally built on other platforms.
macx:SUPPORT_QT_S60 = $$(AUDIOCARVER_WITH_S60)
else:SUPPORT_QT_S60 = 1

# Qt's Q_ASSERT doesn't output file/line on Windows.
# Redefine Q_ASSERT to use mi_assert.
win32: DEFINES *= USE_MI_ASSERT

# GCC 4.6/4.7 have the timespec struct (see also pthreads windows/config.h).
win32-g++ {
    GCC_VERSION = $$system($$QMAKE_CXX -dumpversion)
    contains(GCC_VERSION, ^4\\.[6-7]\\..*): DEFINES *= HAVE_STRUCT_TIMESPEC
}

!verbose: CONFIG *= silent
